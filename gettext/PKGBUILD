# Contributor: Valery Kartel <valery.kartel@gmail.com>
# Maintainer: Carlo Landmeter <clandmeter@gmail.com>
pkgname=gettext
pkgver=0.19.8.1.r35.g89b77b3e
pkgrel=1
pkgdesc="GNU locale utilities"
url="http://www.gnu.org/software/gettext/gettext.html"
arch=('x86_64')
license=('GPL')
makedepends=('perl' 'ncurses' 'libxml2' 'libunistring' 'coreutils')
source=(git://git.sv.gnu.org/$pkgname.git
        git://git.sv.gnu.org/gnulib.git)
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "$pkgname"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname"
  
  sed -i -e "s/wget -q --timeout=5/wget -q -T 5/" autogen.sh
  sed -i -e "s/patch -s/patch/" "$srcdir"/gnulib/gnulib-tool
}

build() {
  cd "$pkgname"
  
  GNULIB_SRCDIR="$srcdir/gnulib" ./autogen.sh --no-git
  ./configure --prefix=/usr \
              --enable-threads=posix \
              --disable-java \
              --disable-static

  make
}

package() {
  cd "$pkgname"
  
  make -j1 DESTDIR="$pkgdir" install
  
  rm "$pkgdir"/usr/lib/charset.alias
}

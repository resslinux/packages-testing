# Contributor: ≈Åukasz Jendrysik <scadu@yandex.com>
# Maintainer: Natanael Copa <ncopa@alpinelinux.org>
pkgbase=bash
pkgname=('bash-doc' 'bash-dev' 'bash')
pkgver=4.4.r12.gbc007799
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
url="http://www.gnu.org/software/bash/bash.html"
arch=('x86_64')
license=('GPL3+')
depends_doc=()
depends_dev=('bash')
depends=('musl' 'ncurses-lib')
makedepends=('readline-dev' 'ncurses-dev' 'bison-dev' 'flex-dev')
source=(git://git.sv.gnu.org/bash.git)
md5sums=('SKIP')

pkgver() {
  cd "$pkgbase"
  
  git describe --long --tags | sed 's/^bash-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgbase"
  
  ./configure --prefix=/usr \
              --bindir=/bin \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --with-curses \
              --disable-nls \
              --enable-readline \
              --without-bash-malloc \
              --with-installed-readline
  
  make
}

package() {
  package-init
  
  cd "$pkgbase"
  
  make DESTDIR="$tmpdir" install
}


package_bash-doc() {
  package-doc
}

package_bash-dev() {
  package-dev
}

package_bash() {
  groups=('base')
  
  package-base
  
  #rm -rf "$pkgdir"/usr/share/locale
  rm -rf "$pkgdir"/usr
  rm "$pkgdir"/bin/bashbug
}

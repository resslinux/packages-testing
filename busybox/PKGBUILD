pkgbase=busybox
pkgname=('busybox-doc' 'busybox')
pkgver=1.28.0.r56.ge411cd071
pkgrel=1
pkgdesc="Size optimized toolbox of many common UNIX utilities"
url="https://busybox.net/"
arch=('x86_64')
license=('GPL2')
depends_doc=()
depends=('musl')
makedepends=('linux-headers')
source=('git://busybox.net/busybox.git'
        'config'
        'clang.patch'
        'acpid.logrotate'
        'securetty'
        'default.script')
sha256sums=('SKIP'
            '96d3c138eb32460b8792aeda115cab8a9d46bcd8c6ee72d335092494019f4181'
            'e5728d8291834c5ea2f71efc5befb4a3c3987c1a6920065afc701959d432e901'
            'f7cbeb5a5a47395ad30454ce8262abcd3e91c33ef803c2ae31a9258d7142dd48'
            'b8c0476e6ba1fcb82449441af35eccae25c1b435c9515051fb68fe964cf720f6'
            '4ad8e0b66706cf413eb7efa3fd5adf06a9bca68121b57b1a458a662e8cffdf88')

pkgver() {
  cd "$pkgbase"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/[_-]/./g'
}

prepare() {
  cd "$pkgbase"
  
  patch -Np0 < "$srcdir/clang.patch"
  
  cp -f ../config .config
  
  #set install directory
  sed -i -e "s|CONFIG_PREFIX=.*|CONFIG_PREFIX=\"$tmpdir\"|" .config
  
  yes "" | make oldconfig
}

build() {
  cd "$pkgbase"
  
  make
}

package() {
  package-init
  
  cd "$pkgbase"
  
  make install
  
  mkdir -p "$tmpdir/usr/sbin" "$tmpdir/usr/bin" "$tmpdir/tmp" \
           "$tmpdir/var/cache/misc" "$tmpdir/bin" "$tmpdir/sbin"

  chmod 1777 "$tmpdir/tmp"
  
  #ifupdown needs those dirs to be present
  mkdir -p "$tmpdir/etc/network/if-down.d" \
           "$tmpdir/etc/network/if-post-down.d" \
           "$tmpdir/etc/network/if-post-up.d" \
           "$tmpdir/etc/network/if-pre-down.d" \
           "$tmpdir/etc/network/if-pre-up.d" \
           "$tmpdir/etc/network/if-up.d"

  install -Dm644 "$srcdir/acpid.logrotate" "$tmpdir/etc/logrotate.d/acpid"
  install -Dm644 "$srcdir/securetty" "$tmpdir/etc/securetty"

  mkdir -p "$tmpdir/var/lib/udhcpd"
  
  install -Dm644 "examples/udhcp/udhcpd.conf" "$tmpdir/etc/udhcpd.conf"
  
  mkdir -p "$tmpdir/usr/share/udhcpc"
  install -Dm755 "$srcdir/default.script" "$tmpdir/usr/share/udhcpc"
  
  mkdir -p "$tmpdir/usr/share/man/man1"
  install -Dm644 "docs/busybox.1" "$tmpdir/usr/share/man/man1"
}

package_busybox-doc() {
  package-doc
}

package_busybox() {
  groups=('base')
  
  package-base
}

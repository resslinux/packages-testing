# Maintainer: Natanael Copa <ncopa@alpinelinux.org>
pkgname=acl
pkgver=2.2.52.r51.g38f32ea
pkgrel=1
pkgdesc="Access control list utilities"
url="http://acl.bestbits.at/"
arch=('x86_64')
license=('LGPL2+')
makedepends=('attr' 'gzip')
source=(git://git.sv.gnu.org/$pkgname.git)
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname"
  
  ./autogen.sh
  
  export INSTALL_USER=root
  export INSTALL_GROUP=root
  
  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --libexecdir=/usr/lib
  make
}

package() {
  cd "$pkgname"
  
  make DESTDIR="$pkgdir" install
}

pkgbase='nano'
pkgname=('nano-doc' 'nano')
pkgver=2.9.4.r2.g1ebb1da3
pkgrel=1
pkgdesc='Text Editor. GNU nano is designed to be a free replacement for the Pico text editor'
url='https://nano-editor.org/'
arch=('x86_64')
license=('GPL')
depends_doc=()
depends=('musl' 'netbsd-curses')
makedepends=('netbsd-curses-dev' 'make' 'clang')
source=('git://git.savannah.gnu.org/nano.git')
sha256sums=('SKIP')

pkgver() {
  cd "${pkgbase}"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgbase}"
  
  ./autogen.sh
  
  ./configure --prefix='/usr' \
              --sysconfdir='/etc' \
              --mandir='/usr/share/man' \
              --infodir='/usr/share/info' \
              --disable-nls
              
  make
}

package() {
  package-init
  
  cd "${pkgbase}"
  
  make DESTDIR="${tmpdir}" install
  
  install -Dm644 "doc/nanorc.sample" "${tmpdir}/etc/nanorc"
}

package_nano-doc() {
  package-doc
}

package_nano() {
  package-base
}

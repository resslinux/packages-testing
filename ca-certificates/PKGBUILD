pkgbase=ca-certificates
pkgname=('ca-certificates-doc' 'ca-certificates')
pkgver=20170717.r3.g55c4565
pkgrel=1
pkgdesc="Common CA certificates PEM files"
url="https://anonscm.debian.org/cgit/collab-maint/ca-certificates.git"
arch=('x86_64')
license=('GPL2+')
depends=('libressl-lib')
makedepends=('python' 'libressl')
#install="ca-certificates.install"
source=('git://anonscm.debian.org/collab-maint/ca-certificates.git'
        'c_rehash.c'
        'certhash'
        '60-ca-certificates.hook')
sha256sums=('SKIP'
            '68f70afada0cd8fc2289102145c11d5d8b6d2421ed4b7624094bcf5dfce7d2f5'
            'dcdcc8690bcb948d9cf90da1f3e6c504273bd13d560edc7482f141ff17acd76a'
            'fcebf7a3342a222e03f0ad9a4de2069c97acdf1ccc2ce55f974d35ad75b3ab10')

pkgver() {
  cd "$pkgbase"
  
  git describe --long --tags | sed 's/^debian\///;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build () {
  cd "$pkgbase"
  
  make
  
  ${CC} ${CFLAGS} -o c_rehash "$srcdir"/c_rehash.c -lcrypto ${LDFLAGS}
}

package() {
  package-init
  
  cd "$pkgbase"
  
  install -d -m755 "$tmpdir/etc/ca-certificates/update.d" \
                   "$tmpdir/usr/bin" \
                   "$tmpdir/usr/sbin" \
                   "$tmpdir/usr/share/ca-certificates" \
                   "$tmpdir/usr/local/share/ca-certificates" \
                   "$tmpdir/etc/ssl/certs"

  make DESTDIR="$tmpdir" install
  
  install -Dm644 sbin/update-ca-certificates.8 "$tmpdir"/usr/share/man/man8/update-ca-certificates.8
                   
  (
    echo "# Automatically generated by ${pkgname}-${pkgver}-${pkgrel}"
    echo "# $(date -u)"
    echo "# Do not edit."
    
    cd "$tmpdir"/usr/share/ca-certificates
    find . -name '*.crt' | sort | cut -b3-
  ) > "$tmpdir"/etc/ca-certificates.conf

  install -Dm755 c_rehash "$tmpdir/usr/bin"
  install -Dm755 "$srcdir/certhash" "$tmpdir/etc/ca-certificates/update.d"
  
  install -d -m755 "$tmpdir/usr/share/libalpm/hooks"
  install -Dm644 "$srcdir/60-ca-certificates.hook" "$tmpdir/usr/share/libalpm/hooks"
}

package_ca-certificates-doc() {
  package-doc
}

package_ca-certificates() {
  package-base
}

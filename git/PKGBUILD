# Contributor: ≈Åukasz Jendrysik <scadu@yandex.com>
# Maintainer: Natanael Copa <ncopa@alpinelinux.org>
pkgbase=git
pkgname=('git-doc'
         'git-svn'
         'git-email'
         'git-fast-import'
         'git-cvs'
         'git-p4'
         'git-daemon'
         'git-subtree'
         'git-subtree-doc'
         'git')
pkgver=2.14.0.r0.g4384e3cde
pkgrel=1
pkgdesc="A distributed version control system"
url="https://www.git-scm.com/"
arch=('x86_64')
license=('GPL2+')
makedepends=('zlib' 'libressl' 'curl' 'expat' 'perl' 'python2' 'pcre' 'asciidoc' 'xmlto' 'perl-error' 'tcl' 'tk')
source=(git://git.kernel.org/pub/scm/git/$pkgbase.git
	bb-tar.patch
	git-daemon.initd
	git-daemon.confd
	)
md5sums=('SKIP'
         '0549894076c2081d886fa5ee44c20e23'
         '14315949509d12efee80285f8a3bc41e'
         '33427921f86acc26d1578d8b308e5baa')

_gitcoredir=/usr/libexec/git-core
_makeopts="
	NO_GETTEXT=YesPlease
	NO_NSEC=YesPlease
	NO_SVN_TESTS=YesPlease
	NO_REGEX=YesPlease
	NO_PYTHON=YesPlease
	NO_PERL=YesPlease
	NO_TCLTK=YesPlease"

pkgver() {
  cd "$pkgbase"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgbase"
  
  make configure
  
  ./configure --prefix=/usr \
              --without-expat \
              --without-tcltk \
              --without-libpcre
  
  make $_makeopts
}

package() {
	package-init
	
	cd "$pkgbase"
	
	make prefix=/usr DESTDIR="$tmpdir" INSTALLDIRS=vendor $_makeopts install
		
	mkdir -p "$tmpdir"/var/git
	
	install -Dm755 "$srcdir"/git-daemon.initd "$tmpdir"/etc/init.d/git-daemon
	install -Dm644 "$srcdir"/git-daemon.confd "$tmpdir"/etc/conf.d/git-daemon

	make prefix=/usr DESTDIR="$tmpdir" install-man
}

package_git-doc() {
  package-doc
}

package_git-email() {
	depends=('perl' 'perl-git=$pkgver-r$pkgrel' 'perl-net-smtp-ssl' 'perl-authen-sasl')
	pkgdesc="Git tools for sending email"
	replaces=('git')
	
	mkdir -p "$pkgdir"/$_gitcoredir
	
	mv "$tmpdir"/$_gitcoredir/*email* "$tmpdir"/$_gitcoredir/*imap* "$pkgdir"/$_gitcoredir
}

package_git-svn() {
	depends=('perl' 'perl-git-svn=$pkgver-r$pkgrel' 'perl-subversion' 'perl-term-readkey')
	pkgdesc="Subversion support for git"
	replaces=()

	mkdir -p "$pkgdir"/$_gitcoredir
	
	mv "$tmpdir"/$_gitcoredir/git-svn "$tmpdir"$_gitcoredir/git-remote-testsvn "$pkgdir"/$_gitcoredir/
}

package_git-cvs() {
	pkgdesc="Git tools for importing CVS repositories"
	depends=('perl' 'perl-git=$pkgver-r$pkgrel' 'cvs' 'perl-dbd-sqlite')
	replaces=('git-perl')
	
	mkdir -p "$pkgdir"/usr/bin "$pkgdir"/$_gitcoredir
	
	mv "$tmpdir"/usr/bin/git-cvs* "$pkgdir"/usr/bin/
	mv "$tmpdir"/$_gitcoredir/*cvs* "$pkgdir"/$_gitcoredir
}

package_git-fast-import() {
	pkgdesc="Git backend for fast Git data importers"
	depends=('git=$pkgver-r$pkgrel')
	
	mkdir -p "$pkgdir"/$_gitcoredir
	
	mv "$tmpdir"/$_gitcoredir/git-fast-import "$pkgdir"/$_gitcoredir/
}

package_git-p4() {
	pkgdesc="Git tools for working with Perforce depots"
	depends=('git=$pkgver-r$pkgrel' 'git-fast-import=$pkgver-r$pkgrel')
	replaces=('git')
	
	mkdir -p "$pkgdir"/$_gitcoredir/mergetools
	
	mv "$tmpdir"/$_gitcoredir/*p4* "$pkgdir"/$_gitcoredir/
	mv "$tmpdir"/$_gitcoredir/mergetools/*p4* "$pkgdir"/$_gitcoredir/mergetools/
}

package_git-daemon() {
	pkgdesc="Git protocol daemon"
	depends=('git=$pkgver-r$pkgrel')
	replaces=('git')
	
	mkdir -p "$pkgdir"/$_gitcoredir
	
	mv "$tmpdir"/$_gitcoredir/git-daemon "$tmpdir"/$_gitcoredir/git-http-backend "$tmpdir"/$_gitcoredir/git-shell "$pkgdir"/$_gitcoredir
	mv "$tmpdir"/etc "$pkgdir"/
}

package_git-subtree() {
	depends=('git=$pkgver-r$pkgrel')
	pkgdesc="Split git repository into subtrees"
	replaces=()

	cd "$srcdir"/"$pkgbase"/contrib/subtree
	
	make prefix=/usr DESTDIR="$tmpdir"
	make install prefix=/usr DESTDIR="$pkgdir"
}

package_git-subtree-doc() {
	depends=()
	pkgdesc="Split git repository into subtrees (documentation)"
	replaces=()

	cd "$srcdir"/"$pkgbase"/contrib/subtree
	make install-man prefix=/usr DESTDIR="$pkgdir"
	gzip -9 "$pkgdir"/usr/share/man/man1/git-subtree.1
}

package_git() {
	package-base
}

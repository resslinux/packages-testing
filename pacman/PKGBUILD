# vim: set ts=2 sw=2 et:
# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>
pkgbase=pacman
pkgname=('pacman-doc' 'pacman-dev' 'pacman')
pkgver=5.0.1.r223.ga7dbe463
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends_doc=()
depends_dev=('pacman')
depends=('libcurl' 'libarchive' 'libressl-lib' 'busybox')
makedepends=('perl' 'pkg-config' 'automake' 'autoconf' 'asciidoc' 'libarchive' 'gettext' 'libtool')
source=('git://git.archlinux.org/pacman.git'
        busybox.patch
        ress.patch
        ress_functions.sh
        pacman.conf
        makepkg.conf)
sha256sums=('SKIP'
            '4ed3d494886237e92eb13eefd0a9e55898305e41cb6f3c30535bf1c6bd0fa7c8'
            '85f90cc7eb2a2df66f8907e2d4e078f957502b39a9b9bcdd531254a6a8fe39b9'
            '064edf1bd6d9ac1ffcd01ba68171f19d0ab596295420043d71874a39634d6b44'
            'a254d0d9ef61e73efdf06ee98ff4565eb69d190034d79e50fa3befdfed4b6535'
            'bddfe59732d3bee28a78fb2be61d2340c4a58eada9d75cbe39d3a27833bd3901')

pkgver() {
  cd "$pkgbase"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgbase"
  
  patch -Np1 -i "$srcdir"/busybox.patch
  patch -Np1 -i "$srcdir"/ress.patch
}

build() {
  cd "$pkgbase"
  
  ./autogen.sh
  
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --enable-doc \
              --with-scriptlet-shell=/bin/sh \
              --with-ldconfig=/usr/bin/ldconfig
              
  make
}

package() {
  package-init
  
  cd "$pkgbase"
  
  make DESTDIR="$tmpdir" install
}

package_pacman-doc() {
  package-doc
}

package_pacman-dev() {
  package-dev
}

package_pacman() {
  groups=('base' 'base-devel')
  backup=(etc/pacman.conf etc/makepkg.conf)
  
  package-base
  
  cd "$pkgbase"
  
  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc/"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc/"

  # put bash_completion in the right location
  install -dm755 "$pkgdir/usr/share/bash-completion/completions"
  mv "$pkgdir/etc/bash_completion.d/pacman" "$pkgdir/usr/share/bash-completion/completions"
  rmdir "$pkgdir/etc/bash_completion.d"

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done
  
  install -m644 "$srcdir/ress_functions.sh" "$pkgdir/usr/share/makepkg/"
  
  #sed -i -e "s/git clone --mirror/git clone --single-branch --mirror/" "$pkgdir/usr/share/makepkg/source/git.sh"
}
